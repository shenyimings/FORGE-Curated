// SPDX-License-Identifier: MIT
pragma solidity 0.8.15;

// Libraries
import { DevFeatures } from "src/libraries/DevFeatures.sol";
import { Constants } from "src/libraries/Constants.sol";

/// @title OPContractsManagerContainer
/// @notice Helper contract that sits alongside the OPContractsManagerV2 contract. This contract
///         MUST be used to store the blueprints and implementations for OPCM because the
///         blueprints and implementations can't be stored as immutables and wouldn't be returned
///         correctly when the OPCM is DELEGATECALLed.
contract OPContractsManagerContainer {
    /// @notice Addresses of the blueprint contracts.
    struct Blueprints {
        address addressManager;
        address proxy;
        address proxyAdmin;
        address l1ChugSplashProxy;
        address resolvedDelegateProxy;
    }

    /// @notice Addresses of the implementation contracts.
    struct Implementations {
        address superchainConfigImpl;
        address protocolVersionsImpl;
        address l1ERC721BridgeImpl;
        address optimismPortalImpl;
        address optimismPortalInteropImpl;
        address ethLockboxImpl;
        address systemConfigImpl;
        address optimismMintableERC20FactoryImpl;
        address l1CrossDomainMessengerImpl;
        address l1StandardBridgeImpl;
        address disputeGameFactoryImpl;
        address anchorStateRegistryImpl;
        address delayedWETHImpl;
        address mipsImpl;
        address faultDisputeGameImpl;
        address permissionedDisputeGameImpl;
        address superFaultDisputeGameImpl;
        address superPermissionedDisputeGameImpl;
        address storageSetterImpl;
    }

    /// @notice Address of the blueprint contracts. This is internal because if it were public the
    ///         autogenerated getter method would return a tuple of addresses, but we want it to
    ///         return a struct.
    Blueprints internal bps;

    /// @notice Address of the implementation contracts. This is internal because if it were public
    ///         the autogenerated getter method would return a tuple of addresses, but we want it to
    ///         return a struct.
    Implementations internal impls;

    /// @notice Bitmap of development features that are enabled. We keep the development feature
    ///         bitmap here rather than in the actual OPCM because other contracts always get a
    ///         reference to this but not to the OPCM itself.
    bytes32 public immutable devFeatureBitmap;

    /// @notice Thrown when a development feature is enabled in production.
    error OPContractsManagerContainer_DevFeatureInProd();

    /// @param _blueprints The blueprint contract addresses.
    /// @param _implementations The implementation contract addresses.
    /// @param _devFeatureBitmap The bitmap of development features that are enabled.
    constructor(Blueprints memory _blueprints, Implementations memory _implementations, bytes32 _devFeatureBitmap) {
        bps = _blueprints;
        impls = _implementations;
        devFeatureBitmap = _devFeatureBitmap;

        // Development features MUST NOT be enabled on Mainnet.
        if (!_isTestingEnvironment() && uint256(_devFeatureBitmap) != 0) {
            revert OPContractsManagerContainer_DevFeatureInProd();
        }
    }

    /// @notice Returns the blueprints.
    /// @return The blueprints.
    function blueprints() public view returns (Blueprints memory) {
        return bps;
    }

    /// @notice Returns the implementations.
    /// @return The implementations.
    function implementations() public view returns (Implementations memory) {
        return impls;
    }

    /// @notice Returns the status of a development feature. Note that this function does not check
    ///         that the input feature represents a single feature and the bitwise AND operation
    ///         allows for multiple features to be enabled at once. Users should generally check
    ///         for only a single feature at a time.
    /// @param _feature The feature to check.
    /// @return True if the feature is enabled, false otherwise.
    function isDevFeatureEnabled(bytes32 _feature) public view returns (bool) {
        return DevFeatures.isDevFeatureEnabled(devFeatureBitmap, _feature);
    }

    /// @notice Returns true if the contract is running in a testing environment. Returns true if
    ///         we're not on mainnet or if the code for the address 0xbeefcafe is not zero. The
    ///         magic address should never have any code in production environments but can be made
    ///         to have code in tests.
    /// @return True if the contract is running in a testing environment, false otherwise.
    function _isTestingEnvironment() internal view returns (bool) {
        return block.chainid != 1 || Constants.TESTING_ENVIRONMENT_ADDRESS.code.length > 0;
    }
}
