// SPDX-License-Identifier: MIT
pragma solidity 0.8.25;

// Libraries
import { SafeCall } from "src/libraries/SafeCall.sol";
import { Predeploys } from "src/libraries/Predeploys.sol";
import { Types } from "src/libraries/Types.sol";

// Interfaces
import { IL2ToL1MessagePasser } from "interfaces/L2/IL2ToL1MessagePasser.sol";
import { IProxyAdmin } from "interfaces/universal/IProxyAdmin.sol";

// External
// import from openzeppelin-contracts-v5
import { Initializable } from "@openzeppelin/contracts-v5/proxy/utils/Initializable.sol";

/// @title FeeVault
/// @notice The FeeVault contract contains the basic logic for the various different vault contracts
///         used to hold fee revenue generated by the L2 system.
abstract contract FeeVault is Initializable {
    /// @notice Error thrown when a function meant to be called by the ProxyAdmin owner is called by another account.
    error FeeVault_OnlyProxyAdminOwner();

    /// @notice The minimum gas limit for the FeeVault withdrawal transaction.
    uint32 internal constant _WITHDRAWAL_MIN_GAS = 400_000;

    /// @notice Total amount of wei processed by the contract.
    uint256 public totalProcessed;

    /// @notice Minimum balance before a withdrawal can be triggered.
    uint256 public minWithdrawalAmount;

    /// @notice Account that will receive the fees. Can be located on L1 or L2.
    address public recipient;

    /// @notice Network which the recipient will receive fees on.
    Types.WithdrawalNetwork public withdrawalNetwork;

    /// @notice Reserve extra slots in the storage layout for future upgrades, 50 in total.
    uint256[46] private __gap;

    /// @custom:legacy
    /// @notice Emitted each time a withdrawal occurs. This event will be deprecated
    ///         in favor of the Withdrawal event containing the WithdrawalNetwork parameter.
    /// @param value Amount that was withdrawn (in wei).
    /// @param to    Address that the funds were sent to.
    /// @param from  Address that triggered the withdrawal.
    event Withdrawal(uint256 value, address to, address from);

    /// @notice Emitted each time a withdrawal occurs.
    /// @param value             Amount that was withdrawn (in wei).
    /// @param to                Address that the funds were sent to.
    /// @param from              Address that triggered the withdrawal.
    /// @param withdrawalNetwork Network which the to address will receive funds on.
    event Withdrawal(uint256 value, address to, address from, Types.WithdrawalNetwork withdrawalNetwork);

    /// @notice Emitted when the minimum withdrawal amount for the vault is updated.
    /// @param oldWithdrawalAmount The previous minimum withdrawal amount.
    /// @param newWithdrawalAmount The new minimum withdrawal amount.
    event MinWithdrawalAmountUpdated(uint256 oldWithdrawalAmount, uint256 newWithdrawalAmount);

    /// @notice Emitted when the fee recipient for this vault is updated.
    /// @param oldRecipient The previous recipient address.
    /// @param newRecipient The new recipient address.
    event RecipientUpdated(address oldRecipient, address newRecipient);

    /// @notice Emitted when the withdrawal network for this vault is updated.
    /// @param oldWithdrawalNetwork The previous withdrawal network.
    /// @param newWithdrawalNetwork The new withdrawal network.
    event WithdrawalNetworkUpdated(
        Types.WithdrawalNetwork oldWithdrawalNetwork, Types.WithdrawalNetwork newWithdrawalNetwork
    );

    /// @notice Constructor for the FeeVault contract.
    ///         This constructor is intentionally empty to prevent initialization.
    ///         Initialization is handled by the `initialize` function.
    constructor() {
        _disableInitializers();
    }

    /// @notice Initializes the FeeVault contract.
    /// @param _recipient           Wallet that will receive the fees.
    /// @param _minWithdrawalAmount Minimum balance for withdrawals.
    /// @param _withdrawalNetwork   Network which the recipient will receive fees on.
    function initialize(
        address _recipient,
        uint256 _minWithdrawalAmount,
        Types.WithdrawalNetwork _withdrawalNetwork
    )
        external
        initializer
    {
        recipient = _recipient;
        minWithdrawalAmount = _minWithdrawalAmount;
        withdrawalNetwork = _withdrawalNetwork;
    }

    /// @notice Allow the contract to receive ETH.
    receive() external payable { }

    /// @notice Updates the minimum amount of funds the FeeVault contract must hold before they can be
    /// withdrawn.
    /// @param _newMinWithdrawalAmount The new minimum withdrawal amount.
    /// @dev If integrating the FeeSplitter contract, the minimum withdrawal amount must be set to 0 to
    /// avoid blocking withdrawals and disbursements for all vaults if one vault doesn't reach the threshold.
    function setMinWithdrawalAmount(uint256 _newMinWithdrawalAmount) external {
        if (msg.sender != IProxyAdmin(Predeploys.PROXY_ADMIN).owner()) {
            revert FeeVault_OnlyProxyAdminOwner();
        }

        uint256 oldWithdrawalAmount = minWithdrawalAmount;
        minWithdrawalAmount = _newMinWithdrawalAmount;

        emit MinWithdrawalAmountUpdated(oldWithdrawalAmount, _newMinWithdrawalAmount);
    }

    /// @notice Updates the recipient of vault fees when they are withdrawn from the vault.
    /// @param _newRecipient The new recipient address.
    function setRecipient(address _newRecipient) external {
        if (msg.sender != IProxyAdmin(Predeploys.PROXY_ADMIN).owner()) {
            revert FeeVault_OnlyProxyAdminOwner();
        }

        address oldRecipient = recipient;
        recipient = _newRecipient;

        emit RecipientUpdated(oldRecipient, _newRecipient);
    }

    /// @notice Updates the network to which vault fees will be withdrawn. This can be either WithdrawalNetwork.L1
    /// to withdraw them to an address on L1 by using the L2ToL1MessagePasser predeploy, or WithdrawalNetwork.L2 to
    /// withdraw them to an address on the same chain.
    /// @param _newWithdrawalNetwork The new withdrawal network.
    function setWithdrawalNetwork(Types.WithdrawalNetwork _newWithdrawalNetwork) external {
        if (msg.sender != IProxyAdmin(Predeploys.PROXY_ADMIN).owner()) {
            revert FeeVault_OnlyProxyAdminOwner();
        }

        Types.WithdrawalNetwork oldWithdrawalNetwork = withdrawalNetwork;
        withdrawalNetwork = _newWithdrawalNetwork;

        emit WithdrawalNetworkUpdated(oldWithdrawalNetwork, _newWithdrawalNetwork);
    }

    /// @notice Triggers a withdrawal of funds to the fee wallet on L1 or L2.
    /// @return value_ The amount of ETH that was withdrawn.
    function withdraw() external returns (uint256 value_) {
        require(
            address(this).balance >= minWithdrawalAmount,
            "FeeVault: withdrawal amount must be greater than minimum withdrawal amount"
        );

        value_ = address(this).balance;
        totalProcessed += value_;

        address recipientAddr = recipient;

        emit Withdrawal(value_, recipientAddr, msg.sender);
        emit Withdrawal(value_, recipientAddr, msg.sender, withdrawalNetwork);

        if (withdrawalNetwork == Types.WithdrawalNetwork.L2) {
            bool success = SafeCall.send(recipientAddr, value_);
            require(success, "FeeVault: failed to send ETH to L2 fee recipient");
        } else {
            IL2ToL1MessagePasser(payable(Predeploys.L2_TO_L1_MESSAGE_PASSER)).initiateWithdrawal{ value: value_ }({
                _target: recipientAddr,
                _gasLimit: _WITHDRAWAL_MIN_GAS,
                _data: hex""
            });
        }
    }

    /// @custom:legacy
    /// @notice Minimum balance before a withdrawal can be triggered.
    ///         Use the `minWithdrawalAmount()` getter as this is deprecated
    ///         and is subject to be removed in the future.
    function MIN_WITHDRAWAL_AMOUNT() public view returns (uint256) {
        return minWithdrawalAmount;
    }

    /// @custom:legacy
    /// @notice Account that will receive the fees. Can be located on L1 or L2.
    ///         Use the `recipient()` getter as this is deprecated
    ///         and is subject to be removed in the future.
    /// @return The recipient address.
    function RECIPIENT() public view returns (address) {
        return recipient;
    }

    /// @custom:legacy
    /// @notice Network which the recipient will receive fees on.
    ///         Use the `withdrawalNetwork()` getter as this is deprecated
    ///         and is subject to be removed in the future.
    function WITHDRAWAL_NETWORK() public view returns (Types.WithdrawalNetwork) {
        return withdrawalNetwork;
    }
}
