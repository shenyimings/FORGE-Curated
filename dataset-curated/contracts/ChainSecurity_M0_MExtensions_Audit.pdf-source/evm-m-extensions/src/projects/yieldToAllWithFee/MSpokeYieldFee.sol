// SPDX-License-Identifier: BUSL-1.1

pragma solidity 0.8.26;

import { MYieldFee } from "./MYieldFee.sol";

import { IContinuousIndexing } from "./interfaces/IContinuousIndexing.sol";
import { IRateOracle } from "./interfaces/IRateOracle.sol";
import { IMSpokeYieldFee } from "./interfaces/IMSpokeYieldFee.sol";

abstract contract MSpokeYieldFeeStorageLayout {
    /// @custom:storage-location erc7201:M0.storage.SpokeMYieldFee
    struct MSpokeYieldFeeStorageStruct {
        address rateOracle;
    }

    // keccak256(abi.encode(uint256(keccak256("M0.storage.MSpokeYieldFee")) - 1)) & ~bytes32(uint256(0xff))
    bytes32 private constant _M_SPOKE_YIELD_FEE_STORAGE_LOCATION =
        0x2aa34bda88d9a6fad4859c438b6b00a9fe5d9d80a401ed1357a646e247972000;

    function _getMSpokeYieldFeeStorageLocation() internal pure returns (MSpokeYieldFeeStorageStruct storage $) {
        assembly {
            $.slot := _M_SPOKE_YIELD_FEE_STORAGE_LOCATION
        }
    }
}

/**
 * @title  SpokeMYieldFee
 * @notice Upgradeable ERC20 Token contract for wrapping M into a non-rebasing token.
 *         Extended to take a fee from the yield generated by M.
 * @dev    All holders of this ERC20 token are earners.
 * @dev    This M extension must only be deployed on Spoke chains.
 *         Check MYieldFee for the Mainnet version.
 * @author M0 Labs
 */
contract MSpokeYieldFee is IMSpokeYieldFee, MSpokeYieldFeeStorageLayout, MYieldFee {
    /* ============ Initializer ============ */

    /**
     * @dev   Initializes the Spoke M Yield Fee token.
     * @param name                  The name of the token (e.g. "M Yield Fee").
     * @param symbol                The symbol of the token (e.g. "MYF").
     * @param mToken                The address of a Spoke M Token.
     * @param swapFacility          The address of the Swap Facility.
     * @param feeRate          The fee rate, in bps, that will be taken from the yield generated by M.
     * @param feeRecipient     The address of the recipient of the yield fee.
     * @param admin                 The address administrating the M extension. Can grant and revoke roles.
     * @param yieldFeeManager       The address managing the fee rate and recipient.
     * @param claimRecipientManager The address managing claim recipients for accounts.
     * @param rateOracle_           The address of a rate oracle.

     */
    function initialize(
        string memory name,
        string memory symbol,
        address mToken,
        address swapFacility,
        uint16 feeRate,
        address feeRecipient,
        address admin,
        address yieldFeeManager,
        address claimRecipientManager,
        address rateOracle_
    ) public virtual initializer {
        if ((_getMSpokeYieldFeeStorageLocation().rateOracle = rateOracle_) == address(0)) revert ZeroRateOracle();

        super.initialize(
            name,
            symbol,
            mToken,
            swapFacility,
            feeRate,
            feeRecipient,
            admin,
            yieldFeeManager,
            claimRecipientManager
        );
    }

    /* ============ View/Pure Functions ============ */

    /// @inheritdoc IMSpokeYieldFee
    function rateOracle() public view returns (address) {
        return _getMSpokeYieldFeeStorageLocation().rateOracle;
    }

    /* ============ Internal View/Pure Functions ============ */

    /**
     * @dev    Returns the timestamp when the earner rate was last accrued to accounts.
     * @dev    Uses `latestUpdateTimestamp` from Spoke M to cap `currentIndex` to the timestamp
     *         when Spoke M's index was last updated and thus avoid overprinting M extension tokens.
     * @return The current block timestamp.
     */
    function _latestEarnerRateAccrualTimestamp() internal view override returns (uint40) {
        return IContinuousIndexing(mToken()).latestUpdateTimestamp();
    }

    /// @dev Returns the current earner rate.
    function _currentEarnerRate() internal view override returns (uint32) {
        // NOTE: The behavior of Rate Oracle is known, so we can safely retrieve the earner rate.
        return IRateOracle(rateOracle()).earnerRate();
    }
}
